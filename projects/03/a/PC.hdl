// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
  IN in[16],load,inc,reset;
  OUT out[16];

  PARTS:
  //16 bit register to store counter
  Register(in=finalValue, load=true, out=RegisterOut, out=out);

  //calculate increment incase inc is true
  Inc16(in=RegisterOut, out=IncRegister);

  //if inc then increment, else stay same
  Mux16(a=RegisterOut, b=IncRegister, sel=inc, out=IncOrSame);

  //if load, then set, else stay same
  Mux16(a=IncOrSame, b=in, sel=load, out=LoadOrSame);

  //if reset, then reset, else stay same
  Mux16(a=LoadOrSame, b=false, sel=reset, out=finalValue);
}
